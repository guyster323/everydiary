plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.everydiary.lite"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    defaultConfig {
        applicationId "com.everydiary.lite"
        minSdk flutter.minSdkVersion
        targetSdk flutter.targetSdkVersion
        versionCode flutter.versionCode
        versionName flutter.versionName
        multiDexEnabled true

        buildConfigField "String", "GEMINI_API_KEY", "\"${project.findProperty('GEMINI_API_KEY') ?: ''}\""
        buildConfigField "String", "HUGGING_FACE_API_KEY", "\"${project.findProperty('HUGGING_FACE_API_KEY') ?: ''}\""
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        profile {
            versionNameSuffix "-profile"
            debuggable false
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    buildFeatures {
        buildConfig true
    }
}

if (System.getenv("GEMINI_API_KEY") != null) {
    project.ext.set("GEMINI_API_KEY", System.getenv("GEMINI_API_KEY"))
}
if (System.getenv("HUGGING_FACE_API_KEY") != null) {
    project.ext.set("HUGGING_FACE_API_KEY", System.getenv("HUGGING_FACE_API_KEY"))
}

flutter {
    source "../.."
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
    implementation "com.google.mlkit:text-recognition-korean:16.0.1"
    implementation "com.google.mlkit:text-recognition-japanese:16.0.1"
    implementation "com.google.mlkit:text-recognition-chinese:16.0.1"
    implementation "com.google.mlkit:text-recognition:16.0.1"
}
